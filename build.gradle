buildscript {
    project.version ='0.1.0-SNAPSHOT'
    ext {
        kotlin_version = '1.2.21'
        atrium_version = '0.6.0'
        mockito_kotlin_version = '1.5.0'
        ghPages_version = project.version
        dokka_version = '0.9.14'
        dokka_sourceMapping = 'tree/master'

        //spek.gradle
        jacoco_tool_version = '0.8.0'
        spek_version = '1.1.5'
        junit_platform_version = '1.1.0'

        //publish.gradle
        bintray_version = '1.7.3'
        publish_pkg_name = project.name
        github_url = "https://github.com/robstoll/${project.name}"
        publish_vcsUrl = "${github_url}.git"

        //gh-pages.gradle
        docProjects = [project]

        projectPrefixed = { String name -> project(":${rootProject.name}-$name") }
        srcKotlin = 'src/main/kotlin'
        srcOwnModule = { String name -> "${rootProject.projectDir}/${rootProject.name}-$name/${ext.srcKotlin}" }
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/robstoll/tutteli-jars" }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile("org.jetbrains.spek:spek-api:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }

    testCompile("com.nhaarman:mockito-kotlin:$mockito_kotlin_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
    compile("ch.tutteli:atrium-cc-en_UK-robstoll:$atrium_version") {
        exclude group: 'org.jetbrains.kotlin'
    }
}

apply from: "${rootProject.projectDir}/gradle/scripts/spek.gradle"

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/kdoc"
    linkMapping {
        dir = "$srcKotlin"
        url = "$github_url/${dokka_sourceMapping}/${project.name}/$srcKotlin"
        suffix = '#L'
    }
}

task sourceJar(type: Jar) {
    from project.sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: dokka) {
    from dokka.outputDirectory
    classifier = 'javadoc'
}
ext {
    publish_artifactId = name
    publish_description = "A set of Spek extensions"
    publish_artifacts = [sourceJar, javadocJar]
}

apply from: "${rootProject.projectDir}/gradle/scripts/publish.gradle"
apply from: 'gradle/scripts/gh-pages.gradle'
